def branch = project.findProperty("commonGradleBranch") ?: "master"
def includeClothConfig = !project.hasProperty("includeClothConfig") || project.includeClothConfig
def testMod = project.findProperty("testMod")

ext {
	testing = false
	defaultCompileDependencies = false
	includeLicense = false
	buildSourcesJar = false
	registerDefaultMavenPublication = false
}

apply from: "https://raw.githubusercontent.com/TheRandomLabs/Common-Gradle/$branch/build.gradle"

if (testMod) {
	sourceSets {
		testmod {
			compileClasspath += main.compileClasspath
			runtimeClasspath += main.runtimeClasspath
		}
	}
}

repositories {
	if (project.hasProperty("clothConfigVersion")) {
		maven {
			url "https://dl.bintray.com/shedaniel/cloth-config-2"
		}
	}

	if (project.hasProperty("autoConfigVersion")) {
		maven {
			url "https://dl.bintray.com/shedaniel/autoconfig1u"
		}
	}

	if (project.hasProperty("cottonClientCommandsVersion")) {
		maven {
			name "CottonMC"
			url "https://server.bbkr.space/artifactory/libs-release"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraftVersion}"
	mappings "net.fabricmc:yarn:${project.yarnMappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricAPIVersion}"

	if (project.hasProperty("modMenuVersion")) {
		modImplementation "io.github.prospector:modmenu:${project.modMenuVersion}"
	}

	if (project.hasProperty("clothConfigVersion")) {
		modImplementation("me.shedaniel.cloth:config-2:${project.clothConfigVersion}") {
			exclude(module: "fabric-api")
		}

		if (includeClothConfig) {
			include "me.shedaniel.cloth:config-2:${project.clothConfigVersion}"
		}
	}

	if (project.hasProperty("autoConfigVersion")) {
		modImplementation("me.sargunvohra.mcmods:autoconfig1u:${project.autoConfigVersion}") {
			exclude(module: "fabric-api")
		}

		if (includeClothConfig) {
			include "me.sargunvohra.mcmods:autoconfig1u:${project.autoConfigVersion}"
		}
	}

	if (project.hasProperty("autoConfigTOMLVersion")) {
		modImplementation "com.github.TheRandomLabs:AutoConfig-TOML:${project.autoConfigTOMLVersion}"
		include "com.github.TheRandomLabs.AutoConfig-TOML:${project.autoConfigTOMLVersion}"
	}

	if (project.hasProperty("cottonClientCommandsVersion")) {
		modImplementation "io.github.cottonmc:cotton-client-commands:${project.cottonClientCommandsVersion}"
	}

	if (testMod) {
		afterEvaluate {
			testmodImplementation(sourceSets.main.output)
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename {
			"${it}_${project.archivesBaseName}"
		}
	}
}

if (testMod) {
	runClient {
		classpath(sourceSets.testmod.runtimeClasspath)
	}

	runServer {
		classpath(sourceSets.testmod.runtimeClasspath)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}

			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}

			artifact javadocJar
		}
	}
}
